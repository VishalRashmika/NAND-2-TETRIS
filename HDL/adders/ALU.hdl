// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
        // zx flag
        Mux16(a=x, b=false, sel=zx, out=out1);
        // nx flag
        Not16(in=out1, out=notx16);
        Mux16(a=out1, b=notx16, sel=nx, out=outx);

        // zy flag
        Mux16(a=y, b=false, sel=zy, out=out3);
        // ny flag
        Not16(in=out3, out=noty16);
        Mux16(a=out3, b=noty16, sel=ny, out=outy);

        // f flag
        And16(a=outx, b=outy, out=outand16);
        Add16(a=outx, b=outy, out=outadd16);
        Mux16(a=outand16, b=outadd16, sel=f, out=fout);

        // // no flag
        // Not16(in=fout, out=outnot16);
        // Mux16(a=fout, b=outnot16, sel=no, out=finout);

        // ng flag
        Not16(in=fout, out=outnot16fin);
        Mux16(a=fout, b=outnot16fin, sel=no, out=out,out[0..7]=zr1,out[8..15]=zr2,out[15]=ng);

        // zr flag
        Or8Way(in=zr1, out=or8way1);
        Or8Way(in=zr2, out=or8way2);
        Or(a=or8way1, b=or8way2, out=outor);
        Not(in=outor, out=zr);
}